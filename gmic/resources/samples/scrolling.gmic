#!/usr/bin/env gmic
# File : scrolling.gmic
# Author : David Tschumperle

# Entrypoint when run from CLI:
go nm Scrolling e[] "" animate 50

# Main function
go :
  v 0
  srand 32

  # Define rendering sizes.
  wf,hf=512,384
  wr,hr={round(1.5*[$wf,$hf])}

  # Generate image [0] = line of text.
  input 0 text "G'MIC rocks !   ",0,0,48,1,1 wt,ht={[w,h]} resize {2.5*$wr},100%,1,1,0,2

  # Generate image [1] = color gradient.
  input {10*ceil($hr/$ht)},1,1,3,u(64,255) tsp[-1] , transpose[-1] resize[-1] $wr,$hr,1,3,3

  # Generate image [2] = 3d cube (3d object).
  box3d {$wr/3} color3d[-1] 255,32,200 center3d[-1]

  # Generate animation frames.
  nbf=50
  repeat $nbf,f
    e[] "\r  > Frame "{$f+1}/$nbf
    input $wr,$hr,1,1
    repeat ceil($hr/$ht)
      image[-1] [0],{w/5*cos($>/3+2*pi*$f/$nbf)-$wt*(1+$f/$nbf)},{$>*$ht}
    done
    +mul[1,-1] remove[-2]
    rotate[-1] 25,1,0
    resize[-1] $wf,$hf,1,100%,0,0,0.5,0.5
    +fill[-1] 0
    +rotate3d[2] 1,-1,0,{180*$f/$nbf} rotate3d[-1] 0,1,0,{360*$f/$nbf} rotate3d[-1] 1,2,3,-60
    object3d[-2] [-1],50%,50%,100,1,3,0,0
    remove[-1] div[-1] 1.6 max[-2,-1]
  done
  keep[3--1]

# End of file.
