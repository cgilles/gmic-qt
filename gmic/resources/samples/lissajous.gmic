#!/usr/bin/env gmic
# File : lissajous.gmic
# Author : David Tschumperle

# Entrypoint when run from CLI:
go nm Lissajous e[] "" animate 60

# Main function
go :
  v 0
  300,1,1,3,u(255) tsp , # Generate random smooth colormap
  repeat 60
    e[] "\r  > Frame "{$>+1}/60
    (0^0^0;64) resize. 300,300,1,3,3 # Generate background image
    eval "
      const dt = "$>"*0.005;
      for (t = b = 0, t<3*pi, t+=0.05, # Draw balls
        x = w/2 + 4*w/9*cos((3.2+3*dt)*(t + dt));
        y = h/2 + 4*h/9*sin((2.1-2*dt)*(t + dt));
        ellipse(x,y,8,8,0,0.4,I[#0,b++]);
        ellipse(x+3,y-3,2,2,0,0.4,255);
      );
    "
    wait 20
  done rm[0]

# End of file.
