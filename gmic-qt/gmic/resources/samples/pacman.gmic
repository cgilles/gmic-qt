#!/usr/bin/env gmic
# File : pacman.gmic
# Author : David Tschumperle

# Entrypoint when run from CLI:
go =>[^] Pacman e[] "" animate 60

# Main function
go :
  v 0

  # Create background image = [0].
  500,300,1,3
  line 0,8%,100%,8%,1,1
  line 0,92%,100%,92%,1,1
  blur y,5%,1 * '[64,32,255]' normalize 0,255

  # Create image of pellets = [1].
  shape_circle 30
  resize. 300%,100%,1,1,0,0,0.5 W:=w
  resize. ..,100%,1,1,0,2

  # Create colormap = [2].
  (0,0,0;255,255,255;255,255,0) permute. yzcx

  nbf=20
  repeat $nbf { f=$>
    e[] "\r  > Frame "{$f+1}/$nbf
    [0],[0] circle. 30%,50%,15%,1,2
    y:=h/2-2*h*($f<$nbf/2?$f:$nbf-$f)/$nbf
    polygon. 3,30%,50%,100%,$y,100%,{h-$y}
    +fill. 0 image. [1],{w/10+2*$W*(1-$f/$nbf)},{(h-h#1)/2}
    max[-2,-1] map. [2] max. [0]
  }
  remove[0-2]

# End of file.
